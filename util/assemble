#! /usr/bin/env scheme-script

#!r6rs
; Copyright 2012 Derick Eddington.  My MIT-style license is in the file named
; LICENSE from the original collection this file is distributed with.

; This program compiles assembly language of the emulated computer, and stores
; the compiled code in new chunks in an emulator storage file.  It can compile
; either the high-level or the low-level languages.  TODO more.

(import (rnrs base)
        (rnrs lists)
        (rnrs io simple)
        (vifne util assembler)
        (vifne util assembler storage)
        (vifne start)
        (vifne config)
        (vifne util command-line))

(define (read-all p)
  (let loop ((a '()))
    (let ((x (read p)))
      (if (eof-object? x)
        (reverse a)
        (loop (cons x a))))))

(define (print n)
  (display "Chunk ID of stored graph:\n")
  (display "#x") (display (number->string n 16)) (newline))


(process-command-line! "-l" "-f" "--magic")

(define language-level
  (command-line-argument "-l"
                         (string->symbol) (lambda (s) (memq s '(low high)))
                         'high))

(define storage-file
  (command-line-argument "-f"
                         () string-non-empty?
                         default-storage-file))

(define magic?
  ; If this is enabled, the assembler allows synthesizing pointers from numbers,
  ; which bypasses capability-security, which sometimes is necessary for
  ; manually constructing a system to run in the emulator.
  (command-line-argument "--magic"
                         (true-string?) boolean?
                         #F))

(define input
  (command-line-argument 'rest
                         (car open-input-file) values
                         (current-input-port)))


(define clean-up (initialize-libraries! storage-file #F))

; Print the chunk ID of the new graph in the mmap'ed storage file, so it can be
; used in some other assembly (e.g. via pointer magic).
(print
 ; Copy the assembled chunk graph into new chunks in the mmap'ed storage file.
 (store!
  ; Assemble an instruction-segment array-tree, or an arbitrary chunk graph,
  ; represented as Scheme-record chunks.
  (assemble-low (case language-level
                  ((high)
                   (assemble-high (read-all input)))
                  ((low)
                   ; Low-level forms must all be in one datum, to distinguish
                   ; between instruction segment or chunk forms.
                   (read input)))
                magic?)))

(clean-up)
